import crimsonedgehope.minecraft.fabric.socksproxyclient.gradle.GenerateTranslateKeysTask
import crimsonedgehope.minecraft.fabric.socksproxyclient.gradle.Tasks

import java.nio.file.Paths

plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
	id "java-library"
	id "com.github.johnrengelman.shadow" version "8.1.1"
	id "org.jetbrains.changelog" version "2.2.0"
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: Tasks

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

apply from: Paths.get("properties.gradle").toString()

base {
	archivesName = project.archives_base_name
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
	it.options.encoding = "UTF-8"
}

sourcesJar {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

shadowJar {
	configurations = [ project.configurations.shadow ]
	archiveClassifier.set("shadow")
}

remapJar {
	dependsOn(shadowJar)
	inputFile = tasks.shadowJar.archiveFile
	archiveClassifier.set("SNAPSHOT")
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url "https://maven.isxander.dev/releases" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabricloader_version}"
	modApi("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude(group: "net.fabricmc")
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("dev.isxander:yet-another-config-lib:${project.yacl_version}") {
		exclude(group: "net.fabricmc")
		exclude(group: "net.fabricmc.fabric-api")
	}
	shadow(implementation("io.netty:netty-handler-proxy:${project.ext.netty_version}"))
	shadow(implementation("dnsjava:dnsjava:${project.ext.dnsjava_version}") {
		exclude(group: "org.slf4j")
	})

	compileOnly "org.projectlombok:lombok:${project.ext.lombok_version}"
	annotationProcessor "org.projectlombok:lombok:${project.ext.lombok_version}"
}

tasks.register("generateTranslateKeys", GenerateTranslateKeysTask) {
	def packageTarget0 = "crimsonedgehope.minecraft.fabric.socksproxyclient.i18n"
	inputFile = file("${rootProject.projectDir}/src/main/resources/assets/socksproxyclient/lang/en_us.json")
	packageTarget = packageTarget0
	outputDir = file("${rootProject.projectDir}/src/main/java/")
}

compileJava.dependsOn generateTranslateKeys
compileJava.mustRunAfter generateTranslateKeys

processResources {
	filesMatching("fabric.mod.json") {
		expand([
				mod_id: mod_id,
				mod_name: archives_base_name,
				mod_version: version,
				minecraft_version: minecraft_dependency,
				modmenu_version: modmenu_version,
				yacl_version: yacl_version,
				fabricloader_version: fabricloader_version,
				github_repository: github_repository
		])
	}

	filesMatching("assets/socksproxyclient/lang/*.json") {
		expand([
		        compilation_java_version: project.ext.compilation_java_version,
				git_hash: project.ext.git_short_hash
		])
	}
}

changelog {
	header.set(provider {
		"${version.get()}"
	})
	combinePreReleases = false
	repositoryUrl = "${github_repository}"
}

modrinth {
	def verbose = Boolean.valueOf(project.getProperties().getOrDefault("verbose", false))
	def noUpload = Boolean.valueOf(project.getProperties().getOrDefault("noUpload", false))

	if (verbose) {
		println "\n========== Properties =========="
		println "Mod version (gradle project.version): ${version}"
		println "Minecraft version: ${minecraft_version}"
		println "YACL version: ${yacl_version}"
		println "Modmenu version: ${modmenu_version}"
		println "========== Properties ==========\n"
	}

	final def modrinth_versionName = "[Fabric ${minecraft_version}] ${version}"
	final def modrinth_gameVersions = modrinth_gameversions.split(",").toList()
	final def modrinth_loaders = ["fabric"]
	def releaseType = project.getProperties().getOrDefault("releaseType", null)
	if (Objects.isNull(releaseType)) {
		if (version.toLowerCase().contains("alpha")) {
			releaseType = "alpha"
		} else if (version.toLowerCase().contains("beta")) {
			releaseType = "beta"
		}
	}
	if (releaseType != "alpha" && releaseType != "beta" && releaseType != "release") {
		throw new IllegalArgumentException("releaseType: alpha, beta or release")
	}

	if (noUpload) {
		println "\n========== Modrinth =========="
		println "Project id: ${modrinth_project_id}"
		println "Version number: ${version}"
		println "Version name: ${modrinth_versionName}"
		println "Release type: ${releaseType}"
		println "Game versions: ${modrinth_gameVersions}"
		println "Loaders: ${modrinth_loaders}"
		println "========== Modrinth ==========\n"
		throw new IllegalArgumentException("no upload")
	}

	token = System.getenv("MODRINTH_TOKEN")
	projectId = "${modrinth_project_id}"
	versionNumber = version
	versionName = modrinth_versionName
	versionType = releaseType
	uploadFile = tasks.remapJar.archiveFile
	gameVersions = modrinth_gameVersions
	loaders = modrinth_loaders
	changelog = "See changelog at " +
			"[GitHub](${github_repository}/releases/tag/v${version})"

	dependencies {
		optional.project "fabric-api"
		optional.project "yacl"
		optional.project "modmenu"
	}
}
