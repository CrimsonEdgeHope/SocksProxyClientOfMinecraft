plugins {
	id "fabric-loom" version "1.4-SNAPSHOT"
	id "java-library"
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
	archiveClassifier.set("shadow")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
	it.options.encoding = "UTF-8"
}

sourcesJar {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

shadowJar {
	configurations = [ project.configurations.shadow ]
	archiveClassifier.set("shadow")
	relocate "io.netty.handler", "crimsonedgehope.minecraft.fabric.socksproxyclient.shadow.io.netty.handler"
}

remapJar {
	dependsOn(shadowJar)
	inputFile = tasks.shadowJar.archiveFile
	archiveClassifier.set("local")
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:11.1.106") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi "com.terraformersmc:modmenu:7.1.0"
	shadow(implementation("io.netty:netty-handler-proxy:4.1.97.Final"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}
